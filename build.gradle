buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

group 'rip.deadcode'
version '0.1-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile(
            'io.ratpack:ratpack-guice:1.5.1',
            'io.ratpack:ratpack-session:1.5.1',

            'com.google.guava:guava:23.6-jre',
            'com.google.code.findbugs:jsr305:3.0.2',
    )
    compileOnly 'org.projectlombok:lombok:1.16.20'
    testCompile(
            'io.ratpack:ratpack-test:1.5.1',

            'junit:junit:4.12',
            'com.google.truth:truth:0.36',
            'org.mockito:mockito-core:2.8.47',
//            'com.google.jimfs:jimfs:1.1'

            'org.slf4j:jul-to-slf4j:1.7.25', // Guava uses java.util.logging
            'ch.qos.logback:logback-classic:1.2.3'
    )
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

bintray {
    user = project.hasProperty('BINTRAY_USER') ? BINTRAY_USER : ''
    key = project.hasProperty('BINTRAY_KEY') ? BINTRAY_KEY : ''
    pkg {
        repo = 'test'
        name = 'ratpack-acsrf'
        licenses = ['MIT']
        vcsUrl = 'https://bitbucket.org/minebreaker_tf/ratpack-acsrf.git'
    }
    publications = ['bintrayPublication']
}

publishing {
    publications {
        bintrayPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}
